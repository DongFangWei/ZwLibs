apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace = "com.dongfangwei.zwlibs"
    compileSdk sdkVersion.compile

    defaultConfig {
        minSdk sdkVersion.min
        targetSdk sdkVersion.target
        versionCode version_code
        versionName version_name

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    api 'androidx.recyclerview:recyclerview:1.3.0'
    api 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    api 'com.squareup.okhttp3:okhttp:3.12.1'
    api 'com.squareup.okhttp3:okhttp-urlconnection:3.12.1'
    api 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    api 'com.github.bumptech.glide:glide:4.11.0'
    api 'com.github.bumptech.glide:okhttp3-integration:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
}

tasks.register('generateSourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.dongfangwei'
                artifactId = 'zwlibs-base'
                version = version_name
            }
        }
        repositories {
            maven {
                //加载资源
                Properties properties = new Properties()
                InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream();
                properties.load(inputStream)

                // 配置仓库路径
                url = properties.getProperty("repo.url")
                // 配置仓库凭证（用户名、密码）
                credentials {
                    // 配置用户名
                    username = properties.getProperty("repo.username")
                    // 配置密码
                    password = properties.getProperty("repo.password")
                }
            }
        }
    }
}